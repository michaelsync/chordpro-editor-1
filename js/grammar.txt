{
    function html_inline(html_class, content){
        var text = '';
        text += '<span class="';
        text += html_class;
        text += '">';
        text += content;
        text += '</span>';
        return text;
    }
    function html_inline_with_controls(html_class, c) {
        var content = html_control(c[0]) + c[1] + html_control(c[2]);
        return html_inline(html_class, content);
    }

    function html_block(html_class, content){
        var text = '';
        text += '<p class="';
        text += html_class;
        text += '">';
        text += content;
        text += '</p>';
        return text;
    }
    function html_control(control_type){
        return html_inline("control-chars", control_type);
    }

}


start
  = c:(block+) n:(newlines_or_not) { return c.join("") + n}


block
  = verse
  / verse_star
  / chorus
  / bridge
  / comment_block


verse
  = c:(newline newlines block_content) { return c[0] + c[1] + html_block('verse', c[2])}

verse_star
  = c:(newline block_content) { return c[0] + html_block('verse_star', c[1])}

chorus
  = c:(newlines_or_not chorus_start block_content chorus_end) { return c[0] + html_block('chorus', html_control(c[1]) + c[2] + html_control(c[3]))}

chorus_start
  = c:("{start_of_chorus}" newline?) { return c.join("")}
  / c:("{soc}" newlines?) { return c.join("")}

chorus_end
  = c:("{end_of_chorus}" newline) { return c.join("")}
  / c:("{eoc}" newline) { return c.join("")}

bridge
  = c:(newlines_or_not bridge_start block_content bridge_end) { return c[0] + html_block('bridge', html_control(c[1]) + c[2] + html_control(c[3]))}

bridge_start
  = c:("{start_of_bridge}" newline?) { return c.join("")}
  / c:("{sob}" newlines?) { return c.join("")}

bridge_end
  = c:("{end_of_bridge}" newline) { return c.join("")}
  / c:("{eob}" newline) { return c.join("")}


guitar_comment_line
  = c:("{guitar_comment:" line_chars "}" newline) { return html_control(c[0]) + c[1] + html_control(c[2]) + c[3]}

comment_block
  = c:(newlines_or_not "{comment:" line_chars "}" newline) { return c[0] + html_control(c[1]) + c[2] + html_control(c[3]) + c[4]}

block_content
  = c:(line_content+) { return c.join("")}

line_content
  = c:(line_chars newline) { return c.join("")}
  / s:(" "*) c:guitar_comment_line { return s.join("") + c}

line_chars
  = c:(line_char+) { return c.join("")}

line_char
  = safe_char
  / chord
  / repeat
  / echo
  / partition

safe_chars_or_chord
  = c:(safe_char_or_chord+) { return c.join("")}

safe_char_or_chord
    = safe_char
    / chord

safe_chars_or_sharp
  = c:(safe_char_or_sharp+) { return c.join("")}

safe_char_or_sharp
  = safe_char
  / "#"

safe_chars
  = c:(safe_char+) { return c.join("")}

safe_char
  = [^\\\n\r{}$\[\]~]

chord
  = c:("[" safe_chars_or_sharp "]") { return html_inline_with_controls('chord', c)}

echo
  = c:("{echo: " safe_chars_or_chord "}") { return html_inline_with_controls('echo', c)}

partition
  = c:("{partition: " safe_chars "}") { return html_inline_with_controls('partition', c)}

repeat
  = c:("{repeat: " digits "}") { return html_inline_with_controls('repeat', c)}

digits
  = c:[0-9]+ {return c.join("")}

newline
  = "\r\n" {return "\n"}
  / "\r" {return "\n"}
  / "\n"

newlines_or_not
  = newlines
  / ""

newlines
  = c:(newline+) { return c.join("")}
